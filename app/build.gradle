apply plugin: 'com.android.application'

android {
  compileSdkVersion 21
  buildToolsVersion "21.1.2"

  defaultConfig {
    applicationId "com.iic.shopingo"
    minSdkVersion 15
    targetSdkVersion 21
    versionCode 1
    versionName "1.0"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  productFlavors {
    localFlavor {
      buildConfigField "String", "SERVER_HOST", "\"${getLocalServerHost()}\""
    }

    herokuFlavor {
      buildConfigField "String", "SERVER_HOST", "\"http://iic-shopingo.herokuapp.com/\""
    }
  }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  compile 'com.android.support:appcompat-v7:21.0.3'
  compile 'com.google.android.gms:play-services-base:6.5.87'
  compile 'com.android.support:cardview-v7:21.0.+'
  compile 'com.google.android.gms:play-services-location:6.5.87'
  compile 'com.jakewharton:butterknife:6.1.0'
  compile 'com.squareup:otto:1.3.6'
  compile 'com.squareup.picasso:picasso:2.5.0'
  compile 'com.facebook.android:facebook-android-sdk:3.23.0'
  compile 'com.google.code.gson:gson:1.7.2'
  testCompile 'org.jmockit:jmockit:1.15'
  testCompile 'junit:junit:4.12'
}

def getLocalServerHost() {
  String ip = getLocalIp();
  return "http://shopingo.${ip}.xip.io/"
}

def getLocalIp() {
  NetworkInterface iface = NetworkInterface.getByName("en0");
  for (InterfaceAddress address : iface.getInterfaceAddresses()) {
    String ip = address.getAddress().getHostAddress();
    if (ip.length() <= 15) {
      return ip;
    }
  }
}